USE [msdb]
GO
/****** Object:  StoredProcedure [dbo].[sp_sp_start_job_wait]    Script Date: 1/25/2023 11:30:17 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

--use msdb

ALTER PROCEDURE [dbo].[sp_sp_start_job_wait_SERVER_45]
(
@job_name SYSNAME,
@WaitTime DATETIME = '00:01:00', 
@JobCompletionStatus INT = null OUTPUT
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
SET NOCOUNT ON

--DECLARE @job_name sysname
DECLARE @job_id UNIQUEIDENTIFIER
DECLARE @job_owner sysname

--Createing TEMP TABLE
--drop table #xp_results
CREATE TABLE #xp_results (job_id UNIQUEIDENTIFIER NOT NULL,
last_run_date INT NOT NULL,
last_run_time INT NOT NULL,
next_run_date INT NOT NULL,
next_run_time INT NOT NULL,
next_run_schedule_id INT NOT NULL,
requested_to_run INT NOT NULL, -- BOOL
request_source INT NOT NULL,
request_source_id sysname COLLATE database_default NULL,
running INT NOT NULL, -- BOOL
current_step INT NOT NULL,
current_retry_attempt INT NOT NULL,
job_state INT NOT NULL)


SELECT @job_id = job_id  from [SPPRDV0045].msdb.dbo.sysjobs where name = @job_name


SELECT @job_owner = SUSER_SNAME()

INSERT INTO #xp_results
EXECUTE [SPPRDV0045].master.dbo.xp_sqlagent_enum_jobs 1,@job_owner

IF NOT EXISTS(SELECT top 1 a.* FROM #xp_results a
inner join [SPPRDV0045].msdb.dbo.sysjobs b on a.job_id = b.job_id
WHERE  name = @job_name and running = 1)

EXEC [SPPRDV0045].msdb.dbo.sp_start_job @job_name = @job_name


WAITFOR DELAY '00:00:03'

DELETE FROM #xp_results
INSERT INTO #xp_results
EXECUTE [SPPRDV0045].master.dbo.xp_sqlagent_enum_jobs 1, @job_owner--, @job_id 

WHILE EXISTS(SELECT top 1 a.* FROM #xp_results a
inner join [SPPRDV0045].msdb.dbo.sysjobs b on a.job_id = b.job_id
WHERE  name = @job_name and running = 1)
BEGIN

WAITFOR DELAY @WaitTime

raiserror('JOB IS RUNNING', 0, 1 ) WITH NOWAIT 

DELETE FROM #xp_results

INSERT INTO #xp_results
EXECUTE [SPPRDV0045].master.dbo.xp_sqlagent_enum_jobs 1, @job_owner--, @job_id 

END


SELECT top 1 @JobCompletionStatus = run_status 
FROM [SPPRDV0045].msdb.dbo.sysjobhistory
WHERE job_id = @job_id
and step_id = 0
order by run_date desc, run_time desc

IF @JobCompletionStatus = 1
PRINT 'The job ran Successful' 
ELSE IF @JobCompletionStatus = 3
PRINT 'The job is Cancelled'
ELSE 
BEGIN
RAISERROR ('[ERROR]:%s job is either failed or not in good state. Please check',16, 1, @job_name) WITH LOG
END

RETURN @JobCompletionStatus
